allprojects {
    defaultTasks 'clean', 'build'

    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'idea'

    buildDir = 'target'
    version = '1.0-SNAPSHOT'
    group = 'org.suggs.test'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    idea {
        downloadSources = true
        downloadJavadoc = true
    }
}

subprojects { subprojects ->

    dependencies {
        compile "org.slf4j:slf4j-api:$slf4jVersion"

        runtime "org.slf4j:slf4j-log4j12:$slf4jVersion"
        runtime "log4j:log4j:$log4jVersion"

        testCompile "junit:junit:$junitVersion"
    }

}

project("sandbox") {
    subprojects {
        dependencies {
            testCompile project(':apps-under-test:trader')
            testCompile project(':apps-under-test:gameoflife')
        }
    }
}

project("sandbox:cucumber") {
    dependencies {
        testCompile "info.cukes:cucumber-junit:$cucumberVersion"
        testCompile "info.cukes:cucumber-java:$cucumberVersion"
    }
}

project("sandbox:jbehave") {
    dependencies {
        testCompile "org.jbehave:jbehave-core:$jbehaveVersion"
    }

}

project("sandbox:concordion") {
    dependencies {
        testCompile "org.concordion:concordion:$concordionVersion"
        testCompile "org.concordion:concordion-extensions:$concordionExtensionsVersion"
        testCompile "org.agileinsider:concordion-plus:$concordionPlusVersion"
    }

    test {
        systemProperties 'concordion.output.dir': file("${buildDir}/concordion-results")
    }
}
